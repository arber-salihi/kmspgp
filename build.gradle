plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "java"
    id "application"
    id "org.graalvm.buildtools.native" version "0.9.4"
}

group "me.stojan.kmspgp"
version "0.1-SNAPSHOT"

application {
    mainClass = "me.stojan.kmspgp.cli.CLI"
}

jar {
    manifest {
        attributes(
                "Main-Class": application.mainClass.get()
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude "META-INF/*"
    exclude "META-INF/**/*"
    exclude "module-info.class"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.slf4j:slf4j-nop:1.7.36"

    implementation platform("software.amazon.awssdk:bom:2.16.60")
    implementation "software.amazon.awssdk:kms"
    implementation "software.amazon.awssdk:apache-client"

    implementation "com.github.ajalt.clikt:clikt:3.4.0"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

test {
    useJUnitPlatform()
}
