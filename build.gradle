plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "java"
    id "application"
}

def determineVersion() {
    def releaseVersion = System.getenv("GITHUB_RELEASE_NAME")
    def githubSha = System.getenv("GITHUB_SHA")

    if (null != releaseVersion) {
        return releaseVersion
    }

    if (null != githubSha) {
        return githubSha
    }

    return "1.0-SNAPSHOT"
}

group "me.stojan.kmspgp"
version determineVersion()

application {
    mainClass = "me.stojan.kmspgp.cli.CLI"
}

jar {
    manifest {
        attributes(
                "Main-Class": application.mainClass.get(),
                "Implementation-Version": determineVersion()
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude "META-INF/*"
    exclude "META-INF/**/*"
    exclude "module-info.class"

    exclude "org/junit/**/*"

    exclude "org/apache/http/**/*"

    exclude "org/bouncycastle/jce/**/*"
    exclude "org/bouncycastle/jcajce/**/*"
    exclude "org/bouncycastle/pqc/**/*"
    exclude "org/bouncycastle/x509/**/*"

    exclude "io/netty/**/*"
}

task("generateBuildInfo") {
    def file = new File(projectDir.absoluteFile, "src/main/kotlin/me/stojan/kmspgp/cli/BuildInfo.kt")

    try {
        file.delete()
    } catch (ignored) {
        // do nothing
    }

    def fileWriter = new FileWriter(file)

    fileWriter.write("object BuildInfo { val version = \"${determineVersion()}\" }")
    fileWriter.flush()
    fileWriter.close()
}

project.tasks.findByName("compileKotlin").dependsOn("generateBuildInfo")

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.slf4j:slf4j-nop:1.7.36"

    implementation platform("software.amazon.awssdk:bom:2.16.60")
    implementation "software.amazon.awssdk:kms"
    implementation "software.amazon.awssdk:url-connection-client"

    implementation "com.github.ajalt.clikt:clikt:3.4.0"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

test {
    useJUnitPlatform()
}
